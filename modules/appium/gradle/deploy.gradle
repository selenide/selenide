jar {
  manifest {
    attributes(
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": version,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

javadoc {
  options.encoding = 'UTF-8'
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

if (project.hasProperty("signing.keyId")) {
  apply plugin: 'signing'
  apply plugin: 'maven-publish'

  signing {
    afterEvaluate {
      sign publishing.publications.mavenJava
    }
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }

  publishing {
    repositories {
      maven {
        name 'Maven'
        url project.version.endsWith("-SNAPSHOT") ?
          'https://oss.sonatype.org/content/repositories/snapshots/' :
          'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials {
          username "$sonatypeUsername"
          password "$sonatypePassword"
        }
      }
    }
    publications {
      mavenJava(MavenPublication) {
        groupId "${project.group}"
        artifactId "${project.name}"

        artifact(jar)
        artifact(sourcesJar)
        artifact(javadocJar)

        pom {
          name = archivesBaseName
          description = 'Selenide adaptor for Appium framework'
          url = 'https://github.com/selenide/selenide-appium'
          licenses {
            license {
              name = 'MIT'
              url = 'https://opensource.org/licenses/MIT'
            }
          }
          developers {
            developer {
              id = 'asolntsev'
              name = 'Andrei Solntsev'
            }
            developer {
              id = 'rosolko'
              name = 'Aliaksandr Rasolka'
            }
            developer {
              id = 'BorisOsipov'
              name = 'Boris Osipov'
            }
          }
          scm {
            connection = 'scm:git@github.com:selenide/selenide-appium.git'
            developerConnection = 'scm:git@github.com:selenide/selenide-appium.git'
            url = 'https://github.com/selenide/selenide-appium'
          }
        }
      }
    }
  }
}
