apply plugin: 'java'
apply plugin: 'maven'

defaultTasks 'check', 'test', 'install'

group = 'com.codeborne'
description = 'Selenide adaptor for Appium framework'
version = '1.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
  encoding = 'UTF-8'
  log4jVersion = '2.10.0'
}

compileJava {
  options.incremental = true
  options.encoding = encoding
}

compileTestJava {
  options.encoding = encoding
}

javadoc {
  options.encoding = encoding
  failOnError = false
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile('com.codeborne:selenide:5.2.3')
  compile('io.appium:java-client:6.1.0')
  testCompile('junit:junit:4.12')
}

task libs(type: Sync) {
  from configurations.compileClasspath
  from configurations.testCompile
  into "$buildDir/lib"
}

compileJava.dependsOn libs

jar {
  manifest {
    attributes(
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": version,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}
