ext {
  artifactId = 'selenide-selenoid'
}

dependencies {
  api project(":statics")
  implementation("org.apache.httpcomponents.client5:httpclient5:$httpClientVersion")
  implementation("commons-io:commons-io:2.21.0")
  implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")

  testImplementation project(':statics').sourceSets.test.output
  testImplementation project(':modules:core').sourceSets.test.output
  testRuntimeOnly("org.seleniumhq.selenium:selenium-devtools-v100:4.2.2") {transitive = false}
  testRuntimeOnly("org.seleniumhq.selenium:selenium-devtools-v128:4.+") {transitive = false}
  testRuntimeOnly project(':modules:proxy')
  testImplementation("org.junit.jupiter:junit-jupiter")
  testImplementation("org.assertj:assertj-core:$assertjVersion")
  testImplementation("org.mockito:mockito-core:$mockitoVersion")
  testRuntimeOnly("org.slf4j:slf4j-simple:$slf4jVersion")
}

tasks.withType(AbstractTestTask).configureEach {
  failOnNoDiscoveredTests = false
}

tasks.register('selenoidTests', Test) {
  systemProperties['selenide.browser'] = 'chrome'
  filter {
    includeTestsMatching "it.selenoid.*"
    excludeTestsMatching "com.codeborne.selenide.*"
    excludeTestsMatching "org.selenide.*"
  }
  outputs.upToDateWhen { false }
  maxParallelForks = 1
}

apply from: rootProject.file('gradle/publish-module.gradle')
