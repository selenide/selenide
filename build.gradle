apply plugin: 'java'
apply plugin: 'maven'

group='com.codeborne'
archivesBaseName = 'selenide'
version='2.11-SNAPSHOT'

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
sourceCompatibility = 1.6
targetCompatibility = 1.6

defaultTasks 'test', 'install'

repositories {
  mavenCentral()
}

configurations {
  provided
  compile.extendsFrom provided
}

dependencies {
  compile('org.seleniumhq.selenium:selenium-java:2.41.0') {
    exclude group: 'org.seleniumhq.selenium', module: 'selenium-htmlunit-driver'
    exclude group: 'org.seleniumhq.selenium', module: 'selenium-android-driver'
    exclude group: 'org.seleniumhq.selenium', module: 'selenium-iphone-driver'
    exclude group: 'org.seleniumhq.selenium', module: 'selenium-safari-driver'
    exclude group: 'org.webbitserver', module: 'webbit'
  }
  provided group: 'org.seleniumhq.selenium', name: 'selenium-htmlunit-driver', version: '2.41.0', transitive: false
  provided group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.14', transitive: false
  testRuntime group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.14'
  testRuntime group: 'org.seleniumhq.selenium', name: 'selenium-safari-driver', version: '2.41.0'
  testRuntime group: 'com.opera', name: 'operadriver', version: '1.5'
  provided 'junit:junit:4.11'
  provided 'org.testng:testng:6.8.8'
  provided group: 'com.github.detro.ghostdriver', name: 'phantomjsdriver', version: '1.1.0', transitive: false
  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile 'org.eclipse.jetty:jetty-server:9.1.4.v20140401'
  testCompile 'org.eclipse.jetty:jetty-servlet:9.1.4.v20140401'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.runtime
  from configurations.testCompile
  from configurations.testRuntime
  into "$buildDir/lib"
}

classes.dependsOn libs

test {
  systemProperties['browser'] = 'htmlunit'
}

task ie(type: Test) {
  systemProperties['browser'] = 'ie'
  systemProperties['timeout'] = '8000'
  systemProperties['webdriver.ie.driver'] = 'C:/Programs/IEDriverServer.exe'
  exclude '**/AlertText.*'
  exclude '**/ConfirmTest.*'
}

task chrome(type: Test) {
  systemProperties['browser'] = 'chrome'
  systemProperties['webdriver.chrome.driver'] = '/usr/bin/chromedriver'
}

task phantomjs(type: Test) {
  systemProperties['browser'] = 'phantomjs'
}

task firefox(type: Test) {
  systemProperties['browser'] = 'firefox'
}

task safari(type: Test) {
  systemProperties['browser'] = 'safari'
}

task opera(type: Test) {
  systemProperties['browser'] = 'opera'
}

tasks.withType(Test).all { testTask ->
  testTask.systemProperties['file.encoding'] = 'UTF-8'
  testTask.testLogging.showStandardStreams = true
  testTask.systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
  testTask.maxParallelForks = 3
  testTask.outputs.upToDateWhen { false }
}

task allTests(dependsOn: ['clean', 'test', 'firefox', 'chrome', 'phantomjs']) {}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

javadoc {
  failOnError=false
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}
