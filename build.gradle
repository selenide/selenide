apply plugin: 'java'
apply plugin: 'maven'

defaultTasks 'check', 'test', 'install'

group = 'com.codeborne'
description = 'Selenide adaptor for Appium framework'
version = '1.4.6-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
  encoding = 'UTF-8'
  log4jVersion = '2.13.0'
}

compileJava {
  options.incremental = true
  options.encoding = encoding
}

compileTestJava {
  options.encoding = encoding
}

javadoc {
  options.encoding = encoding
  failOnError = false
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile('com.codeborne:selenide:5.14.2')
  compile('io.appium:java-client:7.3.0')
  testImplementation("org.junit.jupiter:junit-jupiter:5.7.0")
}

task libs(type: Sync) {
  from configurations.compileClasspath
  from configurations.testCompile
  into "$buildDir/lib"
}

tasks.withType(Test) {
  useJUnitPlatform()
}

compileJava.dependsOn libs

jar {
  manifest {
    attributes(
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": version,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

apply from: file('gradle/deploy.gradle')
