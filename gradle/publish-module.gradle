apply plugin: "com.vanniktech.maven.publish"

ext {
  groupId = 'com.codeborne'
}

base.archivesName.set(artifactId)

tasks.withType(Jar).configureEach {
  manifest {
    attributes(
      "Automatic-Module-Name": project.groupId + '.' + project.artifactId.replace('-', '.'),
      "Implementation-Title": project.groupId + '.' + project.artifactId,
      "Implementation-Version": archiveVersion,
      "Implementation-Vendor": "selenide.org")
  }
}

if (project.hasProperty("signing.keyId")) {
  mavenPublishing {
    coordinates(project.groupId, project.artifactId, project.version)
    pom {
      name = base.archivesName
      description = 'Selenide = concise API for Selenium WebDriver'
      url = 'https://selenide.org'
      inceptionYear = '2011'
      licenses {
        license {
          name = 'MIT'
          url = 'https://opensource.org/licenses/MIT'
        }
      }
      developers {
        developer {
          id = 'asolntsev'
          name = 'Andrei Solntsev'
        }
        developer {
          id = 'BorisOsipov'
          name = 'Boris Osipov'
        }
        developer {
          id = 'rosolko'
          name = 'Aliaksandr Rasolka'
        }
        developer {
          id = 'vinogradoff'
          name = 'Alexei Vinogradov'
        }
        developer {
          id = 'dstekanov'
          name = 'Dmytro Stekanov'
        }
      }
      scm {
        connection = 'scm:git@github.com:selenide/selenide.git'
        developerConnection = 'scm:git@github.com:selenide/selenide.git'
        url = 'https://github.com/selenide/selenide'
      }
    }
  }
}
