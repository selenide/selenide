/**
 * Workaround for configuring SpotBugs {@code effort} and {@code reportLevel}.
 * <p>
 * Due to the way the SpotBugs Gradle plugin loads its classes,
 * direct access via {@code classLoader} is required to properly
 * reference {@code Effort} and {@code Confidence} enums.
 * <p>
 * Standard configuration using string literals or enum values
 * is not supported and will not work as expected.
 * <p>
 * See: <a href="https://github.com/spotbugs/spotbugs-gradle-plugin/issues/972#issuecomment-1769247750">
 * Related discussion and solution on GitHub
 * </a>
 */
def classLoader = plugins['com.github.spotbugs'].class.classLoader
def SpotBugsEffort = classLoader.findLoadedClass('com.github.spotbugs.snom.Effort')
def SpotBugsConfidence = classLoader.findLoadedClass('com.github.spotbugs.snom.Confidence')

subprojects {

  apply plugin: "com.github.spotbugs"

  spotbugs {
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.DEFAULT
    excludeFilter = rootProject.file('config/spotbugs/excludeFilter.xml')
  }

  tasks.named('spotbugsMain').configure {
    reports {
      xml { required = true }
      html {
        required = true
        stylesheet = 'fancy-hist.xsl'
      }
    }
  }

  tasks.named('spotbugsTest').configure {
    reports {
      xml { required = true }
      html {
        required = true
        stylesheet = 'fancy-hist.xsl'
      }
    }
  }

  checkstyle {
    toolVersion = "10.23.0"
  }

  tasks.withType(Checkstyle).configureEach {
    maxWarnings = 0
    showViolations = true
    reports {
      xml.required = System.getenv('CI') != null
      html.required = System.getenv('CI') == null
    }
  }

  sonarqube {
    properties {
      property 'sonar.projectName', 'selenide'
      property 'sonar.jacoco.reportPaths', "$project.buildDir/jacoco/test.exec, $project.buildDir/jacoco/chrome_headless.exec, $project.buildDir/jacoco/firefox_headless.exec"
    }
  }
}
