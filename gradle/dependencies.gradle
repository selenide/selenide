repositories {
  jcenter()
}

ext {
  testngVersion = '6.9.10'
  htmlunitVersion = '2.34.1'
  htmlunitDriverVersion = '2.34.0'
  seleniumVersion = '3.141.59'
  junitVersion = '5.5.0'
  jettyVersion = '9.4.19.v20190610'
}

dependencies {
  api("org.seleniumhq.selenium:selenium-java:$seleniumVersion") {
    exclude group: 'net.bytebuddy', module: 'byte-buddy'
  }
  api('io.github.bonigarcia:webdrivermanager:3.6.1')

  implementation('net.lightbody.bmp:browsermob-core:2.1.5')

  compileOnly("net.sourceforge.htmlunit:htmlunit:$htmlunitVersion") { transitive = false }
  compileOnly("org.seleniumhq.selenium:htmlunit-driver:$htmlunitDriverVersion") { transitive = false }
  compileOnly("org.junit.jupiter:junit-jupiter-api:$junitVersion")
  compileOnly("org.junit.jupiter:junit-jupiter-migrationsupport:$junitVersion")
  compileOnly("org.testng:testng:$testngVersion") { transitive = false }

  testImplementation("org.seleniumhq.selenium:selenium-server:$seleniumVersion") {
    exclude group: 'net.bytebuddy', module: 'byte-buddy'
  }
  testImplementation("org.eclipse.jetty:jetty-servlet:${jettyVersion}")
  testImplementation("org.testng:testng:$testngVersion")
  testImplementation('org.mockito:mockito-core:3.0.0')
  testImplementation('commons-fileupload:commons-fileupload:1.4')
  testImplementation('com.automation-remarks:video-recorder-junit5:2.0')
  testImplementation("org.assertj:assertj-core:3.12.2")

  // for HtmlUnit:
  testImplementation("org.seleniumhq.selenium:htmlunit-driver:$htmlunitDriverVersion") {
    exclude group: 'net.bytebuddy', module: 'byte-buddy'
    exclude group: 'org.eclipse.jetty.websocket'
  }
  testRuntimeOnly("org.eclipse.jetty.websocket:websocket-client:${jettyVersion}")

  testImplementation("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
  testRuntimeOnly('org.slf4j:slf4j-simple:1.7.26')
  testRuntimeOnly('com.codeborne:phantomjsdriver:1.4.4') { transitive = false }
}

task libsProd(type: Sync) {
  from configurations.compileClasspath
  into "$buildDir/lib-prod"
}

task libsTest(type: Sync) {
  from configurations.testRuntimeClasspath - configurations.compileClasspath
  into "$buildDir/lib-test"
}

compileJava.dependsOn libsProd, libsTest
