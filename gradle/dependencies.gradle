ext {
  seleniumVersionStable = '4.40.0'
  seleniumVersion = project.properties['seleniumVersionNightlyBuild'] ?: seleniumVersionStable
  httpClientVersion = '5.6'
  junitVersion = '6.0.2'
  testngVersion = '7.11.0'
  jettyVersion = '11.0.25'
  nettyVersion = '4.2.10.Final'
  assertjVersion = '3.27.7'
  mockitoVersion = '5.21.0'
  slf4jVersion = '2.0.17'
  browserupProxyVersion = '3.2.2'
  littleProxyVersion = '2.6.0'
  jabelVersion = '1.0.0'
  byteBuddyVersion = '1.14.9'
  archunitVersion = '1.4.1'
  jacksonVersion = '2.21.0'
  systemStubsVersion = '2.1.8'
}

subprojects {
  repositories {
    mavenCentral()
    maven {
      name = 'Central Portal Snapshots'
      url = 'https://central.sonatype.com/repository/maven-snapshots/'
      mavenContent {
        snapshotsOnly()
      }
      content {
        includeGroup("org.seleniumhq.selenium")
      }
    }
  }

  configurations.implementation {
    exclude(group: 'com.google.code.findbugs')
  }

  dependencies {
    compileOnly platform("org.junit:junit-bom:$junitVersion")
    testImplementation platform("org.junit:junit-bom:$junitVersion")

    constraints {
      testImplementation("com.github.valfirst.browserup-proxy:browserup-proxy-core:$browserupProxyVersion")
      api("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}") {because 'used by browserup-proxy'}
      testImplementation("io.github.littleproxy:littleproxy:$littleProxyVersion") {because 'used by browserup-proxy'}
    }

    api("org.seleniumhq.selenium:selenium-java:$seleniumVersion") {
      exclude group: 'org.slf4j'
      exclude group: 'com.google.code.findbugs'
    }
    implementation('org.jspecify:jspecify:1.0.0')
    implementation('com.google.errorprone:error_prone_annotations:2.46.0')
    implementation("org.apache.commons:commons-lang3:3.20.0")
    implementation("commons-io:commons-io:2.21.0")
    testImplementation("com.github.valfirst.browserup-proxy:browserup-proxy-core") {
      exclude group: 'xyz.rogfam' // "xyz.rogfam:littleproxy" was moved to "io.github.littleproxy:littleproxy"
    }

    // For testing PDF content
    testImplementation 'com.codeborne:pdf-test:2.1.0'

    testImplementation "org.junit.platform:junit-platform-engine"
    testImplementation "org.junit.platform:junit-platform-launcher"

    testImplementation("io.github.littleproxy:littleproxy") {because 'used by browserup-proxy'}
    testImplementation("io.netty:netty-all:$nettyVersion")
    testImplementation("io.netty:netty-codec:$nettyVersion")
    testImplementation("org.eclipse.jetty:jetty-servlet:${jettyVersion}")
    testImplementation("org.apache.commons:commons-text:1.15.0")
    testImplementation("com.tngtech.archunit:archunit-junit5:${archunitVersion}")
    api("org.slf4j:slf4j-api:$slf4jVersion")
    testRuntimeOnly("org.slf4j:slf4j-simple:$slf4jVersion")
  }
}
