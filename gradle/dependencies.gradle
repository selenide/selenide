repositories {
  jcenter()
}

ext {
  junitVersion = '4.12'
  testngVersion = '6.9.10'
  htmlunitDriverVersion = '2.31.0'
  seleniumVersion = '3.12.0'
}

dependencies {
  api("org.seleniumhq.selenium:selenium-java:$seleniumVersion")
  api('io.github.bonigarcia:webdrivermanager:2.2.1') {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }

  implementation('net.lightbody.bmp:browsermob-core:2.1.5')

  compileOnly("org.seleniumhq.selenium:htmlunit-driver:$htmlunitDriverVersion")
  compileOnly("junit:junit:$junitVersion")
  compileOnly("org.testng:testng:$testngVersion")

  testImplementation("org.seleniumhq.selenium:selenium-server:$seleniumVersion")
  testImplementation('org.eclipse.jetty:jetty-servlet:9.4.9.v20180320')
  testImplementation("junit:junit:$junitVersion")
  testImplementation("org.testng:testng:$testngVersion")
  testImplementation('org.mockito:mockito-core:2.18.3')
  testImplementation('commons-fileupload:commons-fileupload:1.3.3')
  testImplementation('com.automation-remarks:video-recorder-junit:1.8')

  testRuntimeOnly("org.seleniumhq.selenium:htmlunit-driver:$htmlunitDriverVersion")
  testRuntimeOnly('org.slf4j:slf4j-simple:1.7.25')
}

task libsProd(type: Sync) {
  from configurations.compileClasspath
  into "$buildDir/lib-prod"
}

task libsTest(type: Sync) {
  from configurations.testRuntimeClasspath - configurations.compileClasspath
  into "$buildDir/lib-test"
}

compileJava.dependsOn libsProd, libsTest
